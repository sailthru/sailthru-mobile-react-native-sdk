
buildscript {
    ext.kotlin_version = '1.5.31'
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdk 21
        targetSdk 31
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all { test ->
                test.testLogging {
                    events('passed', 'skipped', 'failed', 'standardOut', 'standardError')
                }
            }
        }
    }
    lint {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://github.com/carnivalmobile/maven-repository/raw/master/"
    }
    google()
    maven {
        url "$rootDir/../node_modules/react-native/android"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    api ("com.facebook.react:react-native:+") { // From node_modules
        exclude group:'com.facebook.fbjni'
        exclude group:'com.facebook.yoga'
    }
    api 'com.sailthru.mobile.sdk:sailthru-mobile:17.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.10.0'
    testImplementation 'org.powermock:powermock-core:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.json:json:20210307'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
}
